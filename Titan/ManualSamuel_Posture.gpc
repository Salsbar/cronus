#pragma METAINFO("ManualSamuel_Posture", 1, 1, "Parker")
#include <xb1.gph>
#include "common/ColorLED.gph"

/**
This is a tweaked version of ManualSamuel_IronLung to further help with Amazing Posture and Golden Hair
Check that script out first for useful info (and it's entirely possible to get all 3 with in one go with it)

The main changes are:
    - Auto breathe - now that we don't need to minimise breathes, 
    we can generally handle it programmatically by triggering every 10s
        - NOTE: Manual breathe with dpad down still exists and should be used as a backup.
        - The auto breathe (timer) will reset automatically
    - Toggle auto breathe with dpad right 
        - you'll need to do this when drinking, talking, in the mech, and during cutscenes to avoid muck ups
    - Drink assist - press dpad left
        - during the section in the mech factory, you'll need to drink water to short circuit the door
        - if you miss the timing, you'll fail Golden Hair
        - use when the water is dispesned and ready to pick up via holding LB

Colour key:
    Red = disabled
    Green = enabled/blinking
    Blue = enabled && doing a breath
*/

int loop = 0;
int standing = 0;
int autoBreathe = 0;
uint32 timer = 0;

main {  
    if (event_active(XB1_LS)) {
        loop=!loop;
        standing=loop; // by default, these want to be aligned
        autoBreathe=loop;
        
        // set on trigger, not every loop where true
        if (loop) {
            timer = 0;
            ColorLED(CG); 
        }
    }
    
    // override standing - useful for car
    if (event_active(XB1_RS)) {standing=!standing;}
    if (!standing) { combo_stop(stand); }
    
    // main loop
    if (loop) {
        combo_run(blink);
        if (standing) { combo_run(stand); }
        
        // manually trigger a breath loop
        if (event_active(XB1_DOWN)) { 
            combo_run(breathe);
        }
        // override auto breathe - useful for mech
        if (event_active(XB1_RIGHT)) {
            autoBreathe=!autoBreathe;
        }
        
        // take a break every ~10s
        timer += elapsed_time();
        if (autoBreathe && timer >= 10000) {
            combo_run(breathe);
        }
        
        // drink the water in factory - useful for keeping golden hair
        if (event_active(XB1_LEFT)) {
            combo_run(drink);
        }
    }
    
    // kill switch
    if (!loop) { 
        combo_stop(blink);
        combo_stop(stand);
        combo_stop(breathe);
        timer = 0;
        ColorLED(CR);
    }
    
}

combo breathe {
    ColorLED(CB);
    // in
    set_val(XB1_X, 100);
    wait(2950);
    wait(50);
    //out
    set_val(XB1_B, 100);
    wait(1200);
    wait(50);
    ColorLED(CG);
    timer = 0;
}

combo blink {
    set_val(XB1_A, 100);
    wait(33);
    wait(1000);
}

combo stand {
    set_val(XB1_UP, 100);
    wait(33);
    wait(5000);
}

combo drink {
    set_val(XB1_LB, 100);
    wait(2000);
    set_val(XB1_Y, 100);
    set_val(XB1_LB, 100);
    wait(50);
    set_val(XB1_LB, 100);
    wait(500);
}
