#pragma METAINFO("ManualSamuel", 1, 0, "Parker")
#include <xb1.gph>
#include "common/ColorLED.gph"

/**
I made this to make my life easier doing some of the challenge update e.g. Iron Lung
Initially, there were a lot of issues with delayed or missed inputs.
This was massively improved by reducing the polling rate 125hz.

Use "New And Improved" control scheme (should be the default)

What it does:
Auto blinks (press A every 1s)  - Toggle with LS
                                - This also acts as a master switch
Auto stand (press up every 5s)  - Primarily toggled with LS, but can be overridden with RS. 
                                - this is needed for the car sequence.
Breath assist   - Whilst loop is enabled, press down on the dpad press to trigger
                - Does a deep breath in then medium breath out
                - Use when on deep blue face for maximum breath
                - (Fully automating was highly unreliable due to accrued time error margin
                
Colour key:
    Red = disabled
    Green = enabled/blinking
    Blue = enabled && doing a breath
    Amber = ~40s since last breath (purely an indicator)
*/

int loop = 0;
int standing = 0;
uint32 timer = 0;

main {  
    if (event_active(XB1_LS)) {
        loop=!loop;
        standing=loop; // by default, these want to be aligned
        
        // set on trigger, not every loop where true
        if (loop) {
            timer = 0;
            ColorLED(CG); 
        }
    }
    
    // override standing - useful for car
    if (event_active(XB1_RS)) {standing=!standing;}
    if (!standing) { combo_stop(stand); }
    
    // main loop
    if (loop) {
        combo_run(blink);
        if (standing) { combo_run(stand); }
        
        if (event_active(XB1_DOWN)) { 
            ColorLED(CB);
            combo_run(breathe);
        }
        
        // something I was playing about with but not too important
        timer += elapsed_time();
        if (timer >= 40000) {
            ColorLED(CA);
            timer = 0; // to stop Amber overwriting Blue during breath
        }
    }
    
    // kill switch
    if (!loop) { 
        combo_stop(blink);
        combo_stop(stand);
        combo_stop(breathe);
        timer = 0;
        ColorLED(CR);
    }
    
}

combo breathe {
    // in
    set_val(XB1_X, 100);
    wait(2950);
    wait(50);
    //out
    set_val(XB1_B, 100);
    wait(1200);
    wait(50);
    ColorLED(CG);
    timer = 0;
}

combo blink {
    set_val(XB1_A, 100);
    wait(33);
    wait(1000);
}

combo stand {
    set_val(XB1_UP, 100);
    wait(33);
    wait(5000);
}
